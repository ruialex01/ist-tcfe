.options savecurrents
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 inp_in inv_in vcc vee amp uA741


C1 in inp_in 220n
R1 inp_in 0 1k


R3 inv_in amp 100k

R4 inv_in 0 1k


* load
R2 amp out 1k
C2 out 0 110n

*RL out 0 8

.op
.end

.control

print all

* time analysis
*tran 1e-5 1e-2
*plot v(out)
*hardcopy lab-vo1.ps v(vo) v(vin)
*print v(vin)[45] v(vo)[45]

* frequency analysis
ac dec 10 10 100k

*plot vp(out)
hardcopy phase.ps vp(out)*180/pi

ac dec 10 10 100MEG
hardcopy gain.ps vdb(out)


*Gain, frequency
let gain = vdb(out)-vdb(in)
meas ac gain_db MAX gain

let gain_dev = abs(gain_db - 40)

let range = gain_db - 3

meas ac lowerCOF WHEN gain=range
meas ac higherCOF WHEN gain=range CROSS=LAST

let freq = sqrt(lowerCOF*higherCOF)

let freq_dev = abs(freq - 1000)

let gain_m = 10^(gain_db/20)

let bandwidth=higherCOF-lowerCOF

*merit

let cost = 1.3323e+04+103+3*220e-03
let merit = 1/(cost*(freq_dev+gain_dev+10^-6))

*Input impendance
let Zin = abs(-v(in)[20]/i(Vin)[20])


echo "gain_TAB"
echo "Gain = $&gain_m V"
echo "Gain dB = $&gain_db dB"
echo "Central Frequency = $&freq Hz"
echo "Lower Cut Off Frequency = $&lowerCOF Hz"
echo "Higher Cut Off Frequency = $&higherCOF Hz"
echo "Gain Deviation = $&gain_dev dB"
echo "Frequency Deviation = $&freq_dev Hz"
echo "Bandwidth = $&bandwidth Hz"
echo "gain_END"

echo "merito_TAB"
echo "Cost = $&cost "
echo "Merit = $&merit "
echo "merito_END"

echo "zin_TAB"
echo "Input Impedance = $&Zin "
echo "zin_END"

.endc 
